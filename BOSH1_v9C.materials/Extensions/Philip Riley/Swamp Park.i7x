Swamp Park by Philip Riley begins here.

Include Essentials by Philip Riley.

Include New Light by Philip Riley.    

Include Door Varieties by Philip Riley.                         

Book 1 - Outside HQ 

Swamp Park is a region. 

Chapter 1 - Strip Mall Parking Lot South

The Strip Mall Parking Lot South is a room. The printed name is "south end of the strip mall parking lot". It is east of the front office. It is in Swamp Park. It is outdoors.  The preposition is "at". front office is inside from the Strip Mall Parking Lot South. Understand "end/of/the" as the Strip Mall Parking Lot South.
The description of the Strip Mall Parking Lot South is "From here one can see just how humble BOSH's state has become: the office is to the west, between a pawn shop (to the southwest) and a run-down laundromat (to the northwest) and across from an automatic car wash. To the south is a swampy area separating the lot from the interstate. The parking lot continues north."

Index map with Strip Mall Parking Lot South mapped east of front office.

The Office_ is an unenterable room in Parking Lot South. The printed name is "office". "[We] [gaze] stoically at the sad little storefront that is the new BOSH office. The sign from the previous tenant is still up: 'Angel[']s Department Store'. Through the front window can be seen Margaret, in her usual manic work mode.".
Instead of entering the office_:
	try going west;
	
Margaret at a distance is a part of the Office_.  She is privately-named. The printed name is "Margaret". The description is "Even from here one can sense [Margaret]'s irrepressible energy." Understand "Margaret/Chao" as Margaret at a distance. She is female. She is not neuter.
	
The department store sign is a part of the office_. Understand "angel/angel's" as the department store sign. The description is "It's adorned with a pair of angel's wings."
	 
Understand "office/bosh/hq/headquarters/bureau/storefront", "new office" as the office_.

An automobile is a kind of thing.

Car 1 is an automobile in Strip Mall Parking Lot South. It is privately-named. The printed name is "[our] Hyundai". Understand "car", "hyundai", "automobile", "auto" as car 1. The initial appearance of car 1 is "[Our] Hyundai is parked nearby." It is fixed in place. The description is "An old bright red Hyundai Accent, it's been [our] personal means of transportation for years. And since the Bureau lost its fleet, it's [our] work car too.".

Instead of entering car 1:
	say "[Our] job is here for now.";

Car 2 is an automobile. It is privately-named. The printed name is "[Chief Klimp]'s Porsche". Understand "car/Klimp's/Porsche/automobile/auto" as car 2. The initial appearance of car 2 is "Klimp's Porsche takes up two spots." It is fixed in place. The description is "Klimp drives a sleek 2017 Porsche 911 Turbo Cabriolet."

	
To make is a verb.

Check pushing an automobile: 
	say "[We] could probably push the car if [we] put it in neutral and released the parking brake, but why would [we]?" instead;

Check turning an automobile: 
	say "It's not clear what you mean for [us] to do." instead;
 
The Laundromat-facade is a privately-named unenterable room in Parking Lot South. The printed name is "A-plus laundromat". Understand "A-plus/plus/laundromat" as the laundromat-facade. "[We] [peer] through the windows of the laundromat at the rows of aging machines. A sign on the door says 'Closed for renovations'. [We] [turn] away, unimpressed." 
Instead of entering the laundromat-facade:
	try going northwest;
	
The laundromat sign is a part of the Laundromat-facade. The description is "It's just a piece of stationery marked up with Sharpie and taped to the door. It says 'Closed for renovations'."
	
The laundromat door is a closed and locked scenery shop-door. It is northwest of the parking lot south and southeast of the laundromat-store. The front description is "It's a typical glass door. Taped to it is a sign that says 'Closed for renovations'." The frontside is the strip mall parking lot south. The back description is "It's a typical glass door."
	
Check going northwest from the strip mall parking lot south when the player does not know break-in-to-laundromat: 
	say "A sign on the laundromat door says 'Closed for renovations'." instead;
	
Check going northwest from the strip mall parking lot south when the player knows break-in-to-laundromat: 
	say "Predictably, the laundromat door is locked." instead;
	
The swamp-facade is a privately-named unenterable room in Parking Lot South. The printed name is "swamp". Understand "swamp/marsh/water/wetland/wet/land" as the swamp-facade. "Swamp Park is, as one might expect, dotted with these little bits of wetland."

Instead of entering the swamp-facade:
	try going south;
	
Pawn-Shop-facade is a privately-named unenterable room in Parking Lot South. It is proper-named. The printed name is "Dave's pawn shop". Understand "dave's", "dave", "pawn", "shop" as the pawn-shop-facade. "[We] [observe] the pawn shop with obvious distaste. The flashing neon sign in the window advertises 'Cash for gold!'".

The flashing neon sign is part of the Pawn-Shop-facade. [Understand "pawn/shop/cash/gold" as flashing neon sign.] The description is "Its flash rate is just a bit over the threshold of irritating."
	
The pawn-shop-door is a closed scenery shop-door. It is southwest of the parking lot south and northeast of the Dave's pawn shop. It is privately-named. The printed name is "pawn shop door". Understand "pawn/shop/door" as the pawn-shop-door. The front description is "It's a typical glass door. The light within is too dim to see much." The frontside is the strip mall parking lot south. The back description is "It's a typical glass door."

Instead of entering the pawn-shop-facade:
	try going southwest;
	
[Check going southwest from the strip mall parking lot south when the player does not know pawn-shop-screwdriver:
	say "[We're] not that desperate -- yet." instead;
	
Instead of opening the pawn-shop-door when the player does not know pawn-shop-screwdriver:
	say "[We're] not that desperate -- yet." instead;]

Instead of going nowhere from the Parking Lot South:
	let dir be noun part of current action;
	if dir is south: 
		say "The swamp would just muck up [agent]'s clothes."; 
	otherwise if dir is east:
		say "An automatic car wash isn't of much use when one is on foot.";
	otherwise:
		continue the action;
	stop the action.
	
The automatic car wash is an unenterable room in Strip Mall Parking Lot South. The description is "Much like any other automatic car wash."

Instead of entering the automatic car wash:
	try going east;
	
The interstate is scenery in Strip Mall Parking Lot South. Understand "highway/road/expressway/freeway" as the interstate. "A ways off past the swamp, the highway carries most travelers right past this glum little village."
	
strip mall parking lot south is outside from the Dave's pawn shop.

Chapter 2 - Strip Mall Parking Lot North

The Strip Mall Parking Lot North is a room. It is north of The Strip Mall Parking Lot South. It is in Swamp Park. It is outdoors. The printed name is "north end of the strip mall parking lot". Understand "end/of/the" as the Strip Mall Parking Lot North. The preposition is "at".
"This end is no more auspicious than the other. A convenience store lies to the east. To the west is a military recruitment center. North of here the parking lot exits onto a busy road. There is a rusty sign by the road naming the shopping center: Park Place. South is BOSH headquarters, among other things." 

Understand "Park/place" as Strip Mall Parking Lot North.

Instead of going nowhere from the Parking Lot North:
	let dir be noun part of current action;
	if dir is north:
		say "[We] [have] things to do here.";
	otherwise if dir is west:
		say "[We] will think about joining the military once the Bureau shuts down.";
	otherwise:
		continue the action;
	stop the action.
	
The convenience-store-facade is an unenterable room in strip mall parking lot north. It is privately-named. The printed name is "Li'l Nectarine Convenience Store". Understand "li'l/nectarine/convenience/store" as the convenience-store-facade. "It's marked by a sign with a big nectarine on it."

Instead of entering the convenience-store-facade:
	try going east;
	
The military-facade is an unenterable room in strip mall parking lot north. It is privately-named. The printed name is "Military Recruitment Center". Understand "military/recruitment/center" as the military-facade. "Patriotic posters paper the windows."

Patriotic posters are scenery in the strip mall parking lot north. Understand "military posters", "poster/slogan/slogans" as patriotic posters. "The new slogan is 'Answer the Call!'".

Instead of entering the military-facade:
	try going west;
	
The busy road is scenery in strip mall parking lot north. "The main business thoroughfare of Swamp Park."

The rusty sign is scenery in strip mall parking lot north. "It says 'Park Place'."

BOSH-headquarters-to-the-south is scenery in strip mall parking lot north. It is privately-named. The printed name is "BOSH headquarters". Understand "bosh/headquarters/HQ/office" as BOSH-headquarters-to-the-south. "The BOSH headquarters are to the south."
	
Book 2 - Dave's pawn shop
	
Dave's pawn shop is a room. It is proper-named. "This is more the seedy, gloomy stereotype of a pawn shop than the glitzy sanitized version of TV reality shows. The blinds in the windows are closed to only allow the barest slant of sunlight in, leaving the sickly fluorescent lights to illuminate the room. There is the distinct smell of cigar smoke. The glass counter is filled with a large collection of possibly valuable items. Behind it is an impressive array of firearms."

The destination name is "Dave's pawn shop".

There is a fluorescent light source in Dave's pawn shop called pawn-light.

Dave's pawn shop is in Swamp Park.

A man called Dave is in the pawn shop. Understand "owner" as Dave. "A man, presumably Dave, stands behind the main counter eyeing [Player]." The description is "He's dressed in polyester, as if he were still wearing last night's bowling shirt. A bit of tattoo peeks out from his open collar."

Greeting response for Dave:
	say "[Dave] grunts hello.";
	
[The how-are-you-reply of Dave is "Been better.".]
	
for-a-screwdriver-pawn is a questioning quip.
	It is privately-named. The printed name is "for a screwdriver". 
	Understand "for/a/screwdriver" as for-a-screwdriver-pawn.
	It mentions the screwdriver.
	The comment is "[We] [lean] on the counter and [ask], 'Do you happen to have a screwdriver I could borrow for a few minutes? I'm from the Bureau office next door.'".
	The reply is "The man shakes his head and says, 'Sorry. I had one, but the guy who runs the laundromat borrowed it and didn't bring it back.'".
	It quip-supplies Dave.
	
An availability rule for for-a-screwdriver-pawn:
	if the player knows vent-screws and the screwdriver is not seen:
		always available;
	
After discussing for-a-screwdriver-pawn:
	now player knows screwdriver-in-laundromat;
	continue the action;
	
about the laundromat owner is a questioning quip.
	The comment is "[We] [say], 'How do you suppose I might get in touch with this man who runs the laundromat? I really could use a screwdriver.'".
	The reply is "'Well, that's a tricky one. Laundromat's been closed for almost two weeks now, since the owner just split for some reason he didn't bother to share. Not likely to get ahold of him. If you're not averse to a little trespassing, though, see if you come across a watch. Another thing he borrowed.'".
	It quip-supplies Dave.
	It follows for-a-screwdriver-pawn.
	
After discussing about the laundromat owner:
	now player knows break-in-to-laundromat;
	move Chief Klimp to Chief's office;
	now klimp's door is unlocked;
	now klimp's door is open;
	move car 2 to Strip Mall Parking Lot South;
	continue the action;
	
[An availability rule for about the laundromat owner:
	Unless player knows screwdriver-in-laundromat:
		it is off-limits; 
]
Book 3 - Laundromat

Chapter 1 - The Astral Tunnel

The astral-tunnel-1 is back of the back lot. It is privately-named. The printed name is "astral tunnel". It is in hyperspace. "It is featureless white, or what passes for white here. It bends 'forth' to north.";

The astral-tunnel-2 is north of astral-tunnel-1. It is privately-named. The printed name is "astral tunnel". It is in hyperspace. "It is featureless white, or what passes for white here. It bends south to 'forth.'";

Chapter 2 - The Laundromat Proper

laundromat-region is a region. It is in Swamp Park.

The laundromat-store is a room.
It is privately-named. The printed name is "laundromat". Understand "A-plus/plus/laundromat" as the laundromat-store. It is in laundromat-region.

"Antique washing machines line the walls. A doorway leads west."

The handtruck is in the laundromat-store. It is pushable between rooms.
Understand "hand/truck" as the handtruck.

Instead of taking the handtruck:
	say "The handtruck is a bit unwieldy to carry. Better push it instead.";

Instead of pushing the handtruck to down when the location is the laundromat back room:
	say "The stairs are much too steep to maneuver the handtruck down them.";

Instead of pushing the handtruck to up when the location is the laundromat basement:
	say "The stairs are much too steep to maneuver the handtruck up them.";

The laundromat back room is west of the laundromat-store. It is in laundromat-region.
It is forth of astral-tunnel-2.

"A desk sits in the corner covered with paperwork. A doorway leads east to the laundromat proper, while a steep set of stairs leads down."

The laundromat basement is below the laundromat back room. It is in laundromat-region.

"A few boxes of supplies sit here and there in the otherwise empty space. Stairs lead up, while a doorway leads south."

The back basement is south of the laundromat basement. It is in laundromat-region.

"The basement ends here. There is a switch on the wall, currently [if the light switch is switched on]on[otherwise]off[end if]. A doorway leads north.

[if the broken washing machine is nowhere]A strange glow emanates from behind some stacks of large crates[otherwise]A old broken-down washing machine peeks out from between two stacks of crates[end if]."

After printing the description of a dark room when the location is the back basement and in darkness:
	say line break;
	say "There is a glow-in-the-dark light switch on the wall here, currently [if the light switch is switched on]on[otherwise]off[end if].";
	say line break;
	if the broken washing machine is nowhere:
		say "There is a strange glow emanating from behind some large dark objects.";
	otherwise:
		say "A washing machine is revealed in the darkness by the glow emanating from its drum.";
		
The strange glow is scenery in the back basement. "The glow is blue-white and flickering."
		
After deciding the scope of the player when the location is the back basement and in darkness:
	place the light switch in scope;

laundromat back room is dark.
laundromat basement is dark.
back basement is dark.
There is a fluorescent light source in laundromat-store called ls-light.
There is a fluorescent light source in laundromat back room called lbr-light.
There is a fluorescent light source in laundromat basement called lb-light.
There is a fluorescent light source in back basement called bb-light.

The broken washing machine is an enterable, closed, openable container. It is scenery. "It's a front-loading model. The inside of the drum is obscured by a shimmering curtain of light."

moving it with is an action applying to two things. Understand "move [something] with [something]" as moving it with.

Some stacks of crates are scenery in the back basement.

Check moving something with:
	say "That isn't very helpful.";
	
Instead of moving the stacks of crates with the handtruck:
	if the broken washing machine is nowhere:
		say "You move the crates out of the way and uncover an old front-loading washing machine. There is a strange glow inside the drum.";
		now the broken washing machine is in the back basement;
	otherwise:
		say "There's no point in moving the cabinets around any further.";

After entering the broken washing machine:
	say "There is a flash of light and [we] [find] [ourselves] somewhere else...";
	move the player to the featureless hyperplane;
	
The light switch is a scenery device in the back basement. It is switched on.

After switching off the light switch:
	now ls-light is not lit;
	now lbr-light is not lit;
	now lb-light is not lit;
	now bb-light is not lit;
	continue the action;

After switching on the light switch:
	now ls-light is lit;
	now lbr-light is lit;
	now lb-light is lit;
	now bb-light is lit;
	continue the action;

To remove is a verb.

Every turn:
	if the location is fluorescent-lit and the player is wearing the pair of astral lenses and not in darkness:
		say "Wearing the astral lenses in fluorescent light is blinding. [We] quickly [remove] the glasses.";
		now the hyperlight is unlit;
		now the player carries the pair of astral lenses;
		
	
The astral-tunnel-3 is forth of back basement and backsouth of the laundromat-store. It is privately-named. The printed name is "astral tunnel". It is in hyperspace. "It is featureless white, or what passes for white here. It bends 'back' to 'forthnorth.'";



Book 4 - Convenience Store

The Li'l Nectarine Convenience Store is east of the Parking Lot North. It is in Swamp Park. It is indoors. 
"Li'l Nectarine offers great service and low prices, or so they say. This one's cluttered aisles and sparse shelves could make one wonder if that's true. Behind the counter is the typical array of cigarettes and lottery tickets. Under the counter is a glass case containing various useful items."

The destination name is "Li'l Nectarine Convenience Store".
 
The store clerk is a man in the Convenience Store. "The clerk, a scruffy man in his mid-twenties, is leaning against the counter, looking bored." The description is "The clerk looks as if he'd rather be anywhere else. He's wearing jeans and a white t-shirt with something inscrutable scrawled on it. His hair sticks up every which way." Understand "man" as the store clerk.
The shop counter is a supporter in the nectarine store. It is scenery. "The counter is arrayed with displays of CBD oil, antacids, and the like."

Some CBD oil is scenery in the convenience store. "[We] [don't] need any of this stuff." 

Some antacids are scenery in the convenience store. "[We] [feel] fine for now."

Some cigarettes are scenery in the convenience store. "[We] [have] no interest in smoking." Understand "smokes/smoke/packs/pack" as cigarettes.
Some lottery tickets are scenery in the convenience store. "[We] [feel] no urge to gamble." Understand "scratch-off/scratch-offs/ticket" as lottery tickets.

The store shelves are a thing in the Convenience Store. They are scenery. "[We] hardly [need] bread, ketchup, or potato chips." Understand "aisle/aisles/shelf" as store shelves.

Some groceries are a thing in the Convenience Store. They are scenery. "We're not here to go grocery shopping." Understand "bread/ketchup/catsup/potato/chips" as groceries.

The flashlight is a device. Understand "light/torch" as the flashlight.
Instead of switching on the flashlight:
	now the flashlight is lit.
	
Instead of switching off the flashlight:
	now the flashlight is not lit. 

The glass case is a container in the Convenience Store. it is openable. It is closed and locked. it is scenery. It is transparent. In the glass case is the flashlight, a red screwdriver, a laptop computer, and some wireless earbuds. The wireless earbuds are plural-named. 
Understand "useful/items" as the glass case.
	
The clerk owns the flashlight. The clerk is willing to sell the flashlight.
The price of the flashlight is $5.
The clerk owns the red screwdriver. The clerk is willing to sell the red screwdriver.
The price of the red screwdriver is $5.
The clerk owns the laptop. The clerk is willing to sell the laptop.
The price of the laptop is $1200.  
The clerk owns the earbuds. The clerk is willing to sell the earbuds. 
The price of the earbuds is $85.
   
The description of the red screwdriver is "A medium-length flat head screwdriver with a red plastic handle."
Understand "flat/head/plastic/handle" as the red screwdriver.
The description of the laptop computer is "A knockoff of a fancy gaming laptop." Understand "notebook/computer" as the laptop computer.
The description of the wireless earbuds is "Some no-name brand." Understand "airpods/air/pods/ear/earphones/headphones" as the wireless earbuds.  

 
[Carry out imploring the clerk for something when the flashlight is nowhere during blackout:
	if the topic understood includes "light/flashlight":
		say "The clerk pulls out a flashlight and lays it on the counter. 'Five dollars,' he says."; 
		move the flashlight to the counter.	]	
		
After quizzing the clerk about the glass case:
	say "'You want something inside? Name it.'" instead;
	
[for-a-screwdriver-clerk is a questioning quip.
	It is privately-named. The printed name is "for a screwdriver". [The true-name is "for-a-screwdriver-clerk".] Understand "for/a/screwdriver" as for-a-screwdriver-clerk.
	It mentions the screwdriver.
	The comment is "[We] [ask], 'Would you happen to have a screwdriver I could borrow?'".
	The reply is "'Got one to buy, not one to borrow.'".
	It quip-supplies the clerk.
	
An availability rule for for-a-screwdriver-clerk:	
	if the player knows vent-screws and the screwdriver is not seen:
		always available;]
	
strip mall parking lot north is outside from the the Li'l Nectarine Convenience Store.

Book 5 - Comments about Things

Table of Quiz Topics (continued)
subject (a thing)	interlocutor (a person)	comment (a text)	reply (a text)
glass case	store clerk	--	"'You want something inside, just ask.'"

Book 6 - Polish

Table of Excuses (continued)
source room (a room)	dirs (a list of directions)	excuse (a text)
Strip Mall Parking Lot South	{south, southeast}	"The swamp would just muck up [agent]'s clothes."
Strip Mall Parking Lot South	{east}	"An automatic car wash isn't of much use when one is on foot."
Strip Mall Parking Lot South	{northeast}	"A fence prevents [us] from leaving the property."
Strip Mall Parking Lot North	{northeast, southeast}	"A fence prevents [us] from leaving the property."
Strip Mall Parking Lot North	{northwest, southwest}	"The only thing to the west is the military recruitment center."


Table of Frustrated Taking (continued)
target (a thing)	message (a text)	move message (a text)
stacks of crates	"Those are too heavy to take."	"Those are too heavy to move unaided."


Table of Floors (continued)
locale (a room)	has floor (a truth state)	floor description (a text)	touch description (a text)
strip mall parking lot south	--	"Black asphalt."
strip mall parking lot north	--	"Black asphalt."
li'l nectarine convenience store	--	"Grungy black and white tile."
Dave's pawn shop	--	"Dingy stained industrial carpet."

Swamp Park ends here.
